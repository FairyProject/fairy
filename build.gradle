/*
 * MIT License
 *
 * Copyright (c) 2021 Imanity
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */
//file:noinspection GroovyAssignabilityCheck

plugins {
//    id "io.freefair.aspectj.post-compile-weaving" version "6.1.0" apply false
    id "com.github.johnrengelman.shadow" version "7.0.0" apply false
    id "com.gradle.plugin-publish" version "0.14.0" apply false
}

// Core versioning
version = "0.5.1b41"

allprojects {
    apply from: "${rootDir}/gradle/tasks.gradle"

    if (isNotJavaProject(project)) {
        project.tasks.all { task -> task.enabled = false }
        return
    }
    group = "io.fairyproject"

    repositories {
        mavenCentral()
        maven {
            name = "sonatype-oss-snapshots"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven { url "https://maven.imanity.dev/repository/imanity-libraries/" }
        maven { url "https://nexus.funkemunky.cc/content/repositories/releases/" }
        maven { url "https://jitpack.io" }
        maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
        maven { url "https://repo.viaversion.com/" }
//        maven { url "https://ci.athion.net/job/FastAsyncWorldEdit/ws/mvn/" }
        maven { url "https://maven.sk89q.com/repo/" }
        flatDir { dir("${this.projectDir.path}/libs") }
    }

    ext {
        relocate = "io.fairyproject.libs."

        // Dependencies
        // TODO - naming rules
        aspectjRuntime = "org.aspectj:aspectjrt:" + findProperty("aspectj.version")

        JacksonCore = "com.fasterxml.jackson.core:jackson-core:" + findProperty("jackson.version")
        JacksonDataBind = "com.fasterxml.jackson.core:jackson-databind:" + findProperty("jackson.version")
        JacksonAnnotations = "com.fasterxml.jackson.core:jackson-annotations:" + findProperty("jackson.version")
        JacksonJsr310 = "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:" + findProperty("jackson.version")

        HikariCP = "com.zaxxer:HikariCP:3.4.5"
        MongoDB_driver_sync = "org.mongodb:mongodb-driver-sync:4.0.4"
        Mongojack = "org.mongojack:mongojack:" + findProperty("mongojack.version")
        Redisson = "org.redisson:redisson:3.13.2"
    }

}

subprojects {
    if (isNotJavaProject(project)) {
        project.tasks.all { task -> task.enabled = false }
        project.tasks.withType(PublishToMavenRepository).configureEach { it.enabled = false }
        return
    }

    if (project.name == "gradle-plugin" || project.name == "test-plugin") {
        return
    }

    apply from: "${rootDir}/gradle/builder.gradle"
    apply from: "${rootDir}/gradle/extensions.gradle"
    if (project.name.contains("bootstrap")) {
        apply from: "${rootDir}/gradle/bootstrap.gradle"
    } else {
        apply from: "${rootDir}/gradle/basic.gradle"
        if (project.name.endsWith("-platform")) {
            apply from: "${rootDir}/gradle/platform.gradle"
        } else if (!project.name.endsWith("-tests")) {
            apply from: "${rootDir}/gradle/module.gradle"
        } else {
            version = project(":").version
        }
    }
}

static boolean isNotJavaProject(Project project) {
    return project.buildFile == null || !project.buildFile.exists()
}